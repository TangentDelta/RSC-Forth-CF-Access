HEX
EF00 CONSTANT IOPAGE
IOPAGE 10 + CONSTANT CFBASE
CFBASE 7 + CONSTANT CFREG7

: BYTESWAP ( START-ADDR --- )
  DUP 1+ 2DUP
  C@ SWAP C@
  ROT C! SWAP C!
;

: CFWAIT ( --- )
BEGIN
  CFREG7 C@ 80 AND
  0=
UNTIL
;

: CFERR ( --- )
  CFREG7 C@ 1 AND IF
    ." CF CARD ERROR!"
  THEN
;

: ?CFDRQ ( --- !DRQ )
  CFREG7 C@ 8 AND 0=
;

: CFLBA! ( LBA. --- )
  DUP 100 / CFBASE 3 + C!
  FF AND CFBASE 4 + C!
  DUP 100 / CFBASE 5 + C!
  F AND E0 OR CFBASE 6 + C!
;

: CFREAD ( ADDR --- )
  BEGIN
    DUP
    CFBASE C@ SWAP C! 1+
    ?CFDRQ
  UNTIL

  DROP
;

: CFWRITE ( ADDR --- )
  BEGIN
    DUP ( ADDR ADDR )
    C@ CFBASE C! 1+
    ?CFDRQ
  UNTIL

  DROP
;

: CFINIT ( --- )
  4 CFBASE 7 + C!  ( RESET THE CF CARD )
  CFWAIT

  E0 CFBASE 6 + C!  ( LBA3=0, MASTER, MODE=LBA )

  1 CFBASE 1 + C!   ( 8-BIT MODE )
  EF CFBASE 7 + C!  ( SET FEATURE )

  CFWAIT
  CFERR

  CR ." CF CARD INITIALIZED" CR
;

: CFR/W ( MEMADDR BLOCK# READ --- )
  SWAP  ( MEMADDR READ BLOCK# )
  CFWAIT  ( WAIT FOR CF TO BECOME READY )
  2 U* CFLBA! ( SET THE DESIRED LBA )
  2 CFBASE 2 + C! ( 2 BLOCKS, 1024 BYTES )
  IF
    20 CFREG7 C! ( READ SECTOR COMMAND )
    CFREAD
  ELSE
    30 CFREG7 C! ( WRITE SECTOR COMMAND )
    CFWRITE
  THEN
;

' CFR/W CFA UR/W !  ( ACTIVATE THE CF MASS STORAGE DRIVER )
